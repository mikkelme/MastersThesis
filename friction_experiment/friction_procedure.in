# --- Compute and variables--- #
# Stretch pct
variable stretch_speed equal v_stretch_speed_pct*(v_pullblock_ylo-v_pullblock_yhi) # Henrik used 1,25
variable stretch_time equal $(floor(v_stretch_max_pct/v_stretch_speed_pct/dt))
print "Stretch speed time: $(v_stretch_time) timesteps, $(v_stretch_time*dt) ps"

# Set stretch pct
variable stretch_pct equal 0.0

# Stretch force
compute ylow_force PB_ylow reduce sum fy
compute yhigh_force PB_yhigh reduce sum fy

# --- Output --- #
fix stretch_output all ave/time 1 100 100 v_stretch_pct c_ylow_force c_yhigh_force file stretch.txt
thermo_style custom step time temp v_stretch_pct c_ylow_force c_yhigh_force


# ---  Relax inner part (pattern) of sheet --- #
# Set PB conditions
fix PB_xy_freeze PB_tot move linear 0 0 NULL # Zero out force in x-y-plane
fix PB_z_ave PB_tot aveforce NULL NULL 0 # Average force in z-direction (every atom feels same z-force)

print "Relaxing: $(v_relax_time/dt) timesteps, $(v_relax_time) ps"
run  $(floor(v_relax_time/dt)) 

# ---- Stretch sheet ---- #
# Update PB conditions
unfix PB_xy_freeze
fix ylow_move  PB_ylow move linear 0 $(-v_stretch_speed/2) NULL units box
fix yhigh_move PB_yhigh move linear 0 $(v_stretch_speed/2) NULL units box

# Update stretch pct
variable stretch_start equal $(step)
variable stretch_pct equal v_stretch_speed_pct*(step-v_stretch_start)*dt

print "Stretching: $(v_stretch_time) timesteps, $(v_stretch_time*dt) ps"
run ${stretch_time}


# --- Pause --- #
# Update PB conditions
unfix ylow_move
unfix yhigh_move
velocity PB_tot set 0 0 0
fix PB_xy_freeze PB_tot move linear 0 0 NULL # Zero out force in x-y-plane

# Update stretch pct
variable actual_stretch_max equal $(v_stretch_pct)
variable stretch_pct equal v_actual_stretch_max

print "Pausing, $(v_pause/dt) timesteps, $(v_pause) ps"
run  $(floor(v_pause/dt)) 


#----> Add normal force and relax  <----#

# --- Drag sheet by springforce --- #

# Get position for virtual atom (va)
compute sheet_com sheet com # compute COM for sheet
thermo_style custom c_sheet_com[1] # use compute 
run 0 # Activate the compute by using it
thermo_style custom step time temp # Reset thermo style

# Create virtuel atom
create_atoms 3 single $(c_sheet_com[1]) $(c_sheet_com[2]) $(c_sheet_com[3])
group virtuel_atom type 3

# Add spring between sheet and virtuel atom
fix pull full_sheet spring couple virtuel_atom $(v_K) 0.0 0.0 0.0 0.0

# Move virtuel atom
unfix PB_xy_freeze 
fix move_va virtuel_atom move linear $(v_drag_speed) 0 0 units box # <---- remember to make direction variable
variable drag_time equal $(v_drag_length/v_drag_speed)
print "Drag time, $(v_drag_time/dt) timesteps, $(v_drag_time) ps"
run  $(floor(v_drag_time/dt)) 


